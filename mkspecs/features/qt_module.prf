!contains(QMAKE_INTERNAL_INCLUDED_FILES, .*qmodule\\.pri) {
    QMAKE_QT_MODULE = $$[QT_HOST_DATA/get]/mkspecs/qmodule.pri
    !exists($$QMAKE_QT_MODULE)|!include($$QMAKE_QT_MODULE, "", true) {
        error("Cannot load qmodule.pri!")
    } else {
        QMAKE_QT_MODULE_PRI_LOADED = 1
        debug(1, "Loaded qmodule.pri from ($$QMAKE_QT_MODULE)")
    }
} else {
    QMAKE_QT_MODULE_PRI_LOADED = 1  # covers 'preloading' from .qmake.cache
    debug(1, "Not loading qmodule.pri twice")
}

mac {
   !isEmpty(QMAKE_RPATHDIR){
       CONFIG += absolute_library_soname
   }
}

# Qt modules get compiled without exceptions enabled by default
CONFIG += exceptions_off

#SIMD defines:
sse2:DEFINES += QT_COMPILER_SUPPORTS_SSE2
sse3:DEFINES += QT_COMPILER_SUPPORTS_SSE3
ssse3:DEFINES += QT_COMPILER_SUPPORTS_SSSE3
sse4_1:DEFINES += QT_COMPILER_SUPPORTS_SSE4_1
sse4_2:DEFINES += QT_COMPILER_SUPPORTS_SSE4_2
avx:DEFINES += QT_COMPILER_SUPPORTS_AVX
avx2:DEFINES += QT_COMPILER_SUPPORTS_AVX2
iwmmxt:DEFINES += QT_COMPILER_SUPPORTS_IWMMXT
neon:DEFINES += QT_COMPILER_SUPPORTS_NEON
mips_dsp:DEFINES += QT_COMPILER_SUPPORTS_MIPS_DSP
mips_dspr2:DEFINES += QT_COMPILER_SUPPORTS_MIPS_DSPR2
