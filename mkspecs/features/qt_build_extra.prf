#
#  W A R N I N G
#  -------------
#
# This file is not part of the Qt API.  It exists purely as an
# implementation detail.  It may change from version to version
# without notice, or even be removed.
#
# We mean it.
#

equals(TEMPLATE, subdirs): return()

# It's likely that these extra flags will be wrong for host builds,
# and the bootstrapped tools usually don't need them anyway.
host_build:force_bootstrap: return()

# The headersclean check needs defines and includes even for
# header-only modules.
DEFINES += $$EXTRA_DEFINES
INCLUDEPATH += $$EXTRA_INCLUDEPATH

# The other flags are relevant only for actual libraries.
equals(TEMPLATE, aux): return()

LIBS += $$EXTRA_LIBS

# Static libs need no rpaths
static: return()

QMAKE_RPATHDIR += $$EXTRA_RPATHS
