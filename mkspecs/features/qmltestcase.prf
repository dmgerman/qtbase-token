CONFIG += testcase

!symbian {
    INCLUDEPATH += $$[QT_INSTALL_HEADERS]/QtQuickTest
} else {
    load(data_caging_paths)

    INCLUDEPATH+=$$MW_LAYER_PUBLIC_EXPORT_PATH(QtQuickTest)
}

QT += declarative

win32:CONFIG(debug, debug|release) {
    LIBS += -lQtQuickTest$${QT_LIBINFIX}d
} else {
    LIBS += -lQtQuickTest$${QT_LIBINFIX}
}

# If the .pro file specified an IMPORTPATH, then add that to
# the command-line when the test is run.
!isEmpty(IMPORTPATH) {
    load(testcase)
    for(import, IMPORTPATH): check.commands += -import \"$$import\"
}
DEFINES += QUICK_TEST_SOURCE_DIR=\"\\\"$$OUT_PWD\\\"\"
