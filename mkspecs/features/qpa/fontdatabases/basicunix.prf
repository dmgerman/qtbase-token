#### Remove this define
DEFINES += QT_NO_FONTCONFIG

QT += gui-private core-private

load(qpa/platforms_dir)
load(qpa/harfbuzz_dir)
load(qpa/freetype_dir)
load(qpa/fontengine_dir)


HEADERS += \
        $$QT_PLATFORMS_DIR/fontdatabases/basicunix/qbasicunixfontdatabase.h \
        $$QT_FONTENGINE_DIR/qfontengine_ft_p.h

SOURCES += \
        $$QT_PLATFORMS_DIR/fontdatabases/basicunix/qbasicunixfontdatabase.cpp \
        $$QT_FONTENGINE_DIR/qfontengine_ft.cpp

INCLUDEPATH += $$QT_HARFBUZZ_DIR/src

INCLUDEPATH += $$QT_PLATFORMS_DIR/fontdatabases/basicunix

CONFIG += opentype

contains(QT_CONFIG, freetype) {
    SOURCES += \
               $$QT_FREETYPE_DIR/src/base/ftbase.c \
               $$QT_FREETYPE_DIR/src/base/ftbbox.c \
               $$QT_FREETYPE_DIR/src/base/ftdebug.c \
               $$QT_FREETYPE_DIR/src/base/ftglyph.c \
               $$QT_FREETYPE_DIR/src/base/ftinit.c \
               $$QT_FREETYPE_DIR/src/base/ftmm.c \
               $$QT_FREETYPE_DIR/src/base/fttype1.c \
               $$QT_FREETYPE_DIR/src/base/ftsynth.c \
               $$QT_FREETYPE_DIR/src/base/ftbitmap.c \
               $$QT_FREETYPE_DIR/src/bdf/bdf.c \
               $$QT_FREETYPE_DIR/src/cache/ftcache.c \
               $$QT_FREETYPE_DIR/src/cff/cff.c \
               $$QT_FREETYPE_DIR/src/cid/type1cid.c \
               $$QT_FREETYPE_DIR/src/gzip/ftgzip.c \
               $$QT_FREETYPE_DIR/src/pcf/pcf.c \
               $$QT_FREETYPE_DIR/src/pfr/pfr.c \
               $$QT_FREETYPE_DIR/src/psaux/psaux.c \
               $$QT_FREETYPE_DIR/src/pshinter/pshinter.c \
               $$QT_FREETYPE_DIR/src/psnames/psmodule.c \
               $$QT_FREETYPE_DIR/src/raster/raster.c \
               $$QT_FREETYPE_DIR/src/sfnt/sfnt.c \
               $$QT_FREETYPE_DIR/src/smooth/smooth.c \
               $$QT_FREETYPE_DIR/src/truetype/truetype.c \
               $$QT_FREETYPE_DIR/src/type1/type1.c \
               $$QT_FREETYPE_DIR/src/type42/type42.c \
               $$QT_FREETYPE_DIR/src/winfonts/winfnt.c \
               $$QT_FREETYPE_DIR/src/lzw/ftlzw.c\
               $$QT_FREETYPE_DIR/src/otvalid/otvalid.c\
               $$QT_FREETYPE_DIR/src/otvalid/otvbase.c\
               $$QT_FREETYPE_DIR/src/otvalid/otvgdef.c\
               $$QT_FREETYPE_DIR/src/otvalid/otvjstf.c\
               $$QT_FREETYPE_DIR/src/otvalid/otvcommn.c\
               $$QT_FREETYPE_DIR/src/otvalid/otvgpos.c\
               $$QT_FREETYPE_DIR/src/otvalid/otvgsub.c\
               $$QT_FREETYPE_DIR/src/otvalid/otvmod.c\
               $$QT_FREETYPE_DIR/src/autofit/afangles.c\
               $$QT_FREETYPE_DIR/src/autofit/afglobal.c\
               $$QT_FREETYPE_DIR/src/autofit/aflatin.c\
               $$QT_FREETYPE_DIR/src/autofit/afmodule.c\
               $$QT_FREETYPE_DIR/src/autofit/afdummy.c\
               $$QT_FREETYPE_DIR/src/autofit/afhints.c\
               $$QT_FREETYPE_DIR/src/autofit/afloader.c\
               $$QT_FREETYPE_DIR/src/autofit/autofit.c

               symbian {
                   SOURCES += \
                              $$QT_FREETYPE_DIR/src/base/ftsystem.c
               } else {
                   SOURCES += \
                              $$QT_FREETYPE_DIR/builds/unix/ftsystem.c
                  INCLUDEPATH += \
                              $$QT_FREETYPE_DIR/builds/unix
               }

               INCLUDEPATH += \
                   $$QT_FREETYPE_DIR/src \
                   $$QT_FREETYPE_DIR/include

               DEFINES += FT2_BUILD_LIBRARY
               contains(QT_CONFIG, system-zlib) {
                    DEFINES += FT_CONFIG_OPTION_SYSTEM_ZLIB
               }

    } else:contains(QT_CONFIG, system-freetype) {
        # pull in the proper freetype2 include directory
        #include($$QT_SOURCE_TREE/config.tests/unix/freetype/freetype.pri)
        !cross_compile {
            TRY_INCLUDEPATHS = /include /usr/include $$QMAKE_INCDIR $$QMAKE_INCDIR_X11 $$INCLUDEPATH
            # LSB doesn't allow using headers from /include or /usr/include
            linux-lsb-g++:TRY_INCLUDEPATHS = $$QMAKE_INCDIR $$QMAKE_INCDIR_X11 $$INCLUDEPATH
            for(p, TRY_INCLUDEPATHS) {
                p = $$join(p, "", "", "/freetype2")
                exists($$p):INCLUDEPATH *= $$p
            }
        }
        LIBS_PRIVATE += -lfreetype
    }

