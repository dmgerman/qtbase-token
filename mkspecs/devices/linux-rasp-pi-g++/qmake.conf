#
# qmake configuration for Broadcom's Raspberry PI
# http://wiki.qt-project.org/Devices/RaspberryPi

!exists($$[QT_SYSROOT]/usr/include/libudev.h) {
    message("Keyboard support requires udev")
    message("Install libudev-dev in your rootfs")
    error()
}

MAKEFILE_GENERATOR  = UNIX
TARGET_PLATFORM     = unix
TEMPLATE            = app
CONFIG              += qt warn_on release incremental link_prl gdb_dwarf_index
QT                  += core gui
QMAKE_INCREMENTAL_STYLE = sublib

include(../../common/linux.conf)
include(../../common/gcc-base-unix.conf)
include(../../common/g++-unix.conf)

load(device_config)

QMAKE_CC                = $${CROSS_COMPILE}gcc
QMAKE_CXX               = $${CROSS_COMPILE}g++
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

# modifications to linux.conf
QMAKE_AR                = $${CROSS_COMPILE}ar cqs
QMAKE_OBJCOPY           = $${CROSS_COMPILE}objcopy
QMAKE_STRIP             = $${CROSS_COMPILE}strip

#Circular dependency in GLES2 <-> EGL requires the following abomination
QMAKE_LIBS_EGL          = -L$$[QT_SYSROOT]/opt/vc/lib -lWFC -lGLESv2 -lEGL -lbcm_host -lopenmaxil
QMAKE_LIBS_OPENGL_ES2   = $${QMAKE_LIBS_EGL}
QMAKE_LIBS_OPENVG       = $${QMAKE_LIBS_EGL}

QMAKE_INCDIR_EGL        = $$[QT_SYSROOT]/opt/vc/include
QMAKE_INCDIR_OPENGL_ES2 = $${QMAKE_INCDIR_EGL}

QMAKE_CFLAGS_RELEASE    += \
                          -mfloat-abi=softfp \
                          -mfpu=vfp \
                          -mtune=arm1176jzf-s \
                          -march=armv6zk \
                          -mabi=aapcs-linux

QMAKE_CXXFLAGS_RELEASE = $$QMAKE_CFLAGS_RELEASE

EGLFS_PLATFORM_HOOKS_SOURCES = $$PWD/qeglfshooks_pi.cpp

# Sanity check
deviceSanityCheckCompiler()

load(qt_config)
