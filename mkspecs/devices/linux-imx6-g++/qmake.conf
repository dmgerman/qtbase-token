#
# qmake configuration for the Freescale iMX6 boards (single, dual and quad)
#
# This mkspec is based and tested on the ltib-source-release 3.0.15
# Building ltib including glib, gpu-drivers and what ever you want to use (e.g. dbus or fontconfig)
# This mkspec is for the fb-version (eglfs), and not for x11
# A typical configure line looks like:
# /home/tsenyk/qt5/qt5/configure -opensource -confirm-license -make libs -device imx6 \
# -device-option CROSS_COMPILE=/opt/freescale/usr/local/gcc-4.6.2-glibc-2.13-linaro-multilib-2011.12/fsl-linaro-toolchain/bin/arm-fsl-linux-gnueabi- \
# -device-option ROOTFS=<path-to-your-ltib-install>/rootfs \
# -prefix /usr/local/Trolltech/Qt5-imx6-1
#
# NOTE:  currently ltib doesn't produce a proper staging/sysroot-filessystem.
#        If one sets -sysroot <path-to-rootfs>, the toolchain doesn't find basic binaries (e.g. crt1.o),
#        therefore we can't use -sysroot for this device.
#        The solution for this platform is to use a seperate device-option instead of sysroot:
#           -device-option ROOTFS=<path-to-ltib-install>/rootfs

include(../common/linux_device_pre.conf)

EGLFS_PLATFORM_HOOKS_SOURCES = $$PWD/qeglfshooks_imx6.cpp

QMAKE_INCDIR            = $${ROOTFS}/usr/include \
                          $${ROOTFS}/usr/include/dbus-1.0 \
                          $${ROOTFS}/usr/lib/dbus-1.0/include \
                          $${ROOTFS}/usr/include/freetype2
QMAKE_INCDIR_OPENGL_ES2 = $${ROOTFS}/usr/include/GLES2


### LIBS
QMAKE_LIBDIR            = $${ROOTFS}/usr/lib

QMAKE_LIBS_EGL          = -lEGL
QMAKE_LIBS_OPENGL_ES2   = -lGLESv2 -lEGL -lGAL
QMAKE_LIBS_OPENVG       = -lOpenVG -lEGL -lGAL


### additional FLAGS
QMAKE_LFLAGS            += -Wl,-rpath-link,$${ROOTFS}/usr/lib

QMAKE_CFLAGS_RELEASE    = -O2 -march=armv7-a -mfpu=neon -DLINUX=1 -DEGL_API_FB=1
QMAKE_CXXFLAGS_RELEASE  = $$QMAKE_CFLAGS_RELEASE

include(../common/linux_device_post.conf)

load(qt_config)
