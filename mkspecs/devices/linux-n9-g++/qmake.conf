#
# qmake configuration for building Qt5 for the Nokia N9
# http://wiki.qt-project.org/Devices/N9

MAKEFILE_GENERATOR      = UNIX
TARGET_PLATFORM         = unix
TEMPLATE                = app
CONFIG                  += qt warn_on release incremental link_prl gdb_dwarf_index
QT                      += core gui
QMAKE_INCREMENTAL_STYLE = sublib

include(../../common/linux.conf)
include(../../common/gcc-base-unix.conf)
include(../../common/g++-unix.conf)

load(device_config)

# modifications to g++.conf
QMAKE_CC                = $${CROSS_COMPILE}gcc
QMAKE_CXX               = $${CROSS_COMPILE}g++
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

# modifications to linux.conf
QMAKE_AR                = $${CROSS_COMPILE}ar cqs
QMAKE_OBJCOPY           = $${CROSS_COMPILE}objcopy
QMAKE_STRIP             = $${CROSS_COMPILE}strip

QMAKE_CFLAGS_RELEASE   += -O2 -mfloat-abi=hard -mfpu=neon -march=armv7-a -mcpu=cortex-a8
QMAKE_CXXFLAGS_RELEASE += $$QMAKE_CFLAGS_RELEASE

QT_QPA_DEFAULT_PLATFORM = xcb

deviceSanityCheckCompiler()

load(qt_config)
